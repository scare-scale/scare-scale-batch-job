plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'com.scarescale'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Define dependencies for the application at root level
dependencies {
    // Include all modules
    implementation project(':dispatcher')
    implementation project(':movie-update-job')
}

application {
    mainClass = 'com.scarescale.batch.BatchDispatcher'
}

test {
    useJUnitPlatform()
}

tasks.register('runBatch', JavaExec) {
    group = "Execution"
    description = "Runs the Scare Scale Batch Application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
}

// Task for creating a fat JAR for AWS Lambda deployment
shadowJar {
    archiveFileName = "scare-scale-batch-job-all.jar"
    archiveClassifier = ""
    mergeServiceFiles()

    // Include all submodule classes and resources
    from project(':dispatcher').sourceSets.main.output
    from project(':movie-update-job').sourceSets.main.output
}

// Make shadowJar part of the build
build.dependsOn shadowJar