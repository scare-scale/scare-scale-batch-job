plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'com.scarescale'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0'

    implementation 'com.amazonaws:aws-lambda-java-core:1.4.0'

    implementation 'com.google.auto.service:auto-service:1.1.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'

    implementation "uk.co.conoregan:themoviedbapi:2.4.0"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.11.0'
}

application {
    mainClass = 'com.scarescale.batch.ScareScaleBatchApplication'
}

test {
    useJUnitPlatform()
}

tasks.register('runBatch', JavaExec) {
    group = "Execution"
    description = "Runs the Scare Scale Batch Application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
}

// Task for creating a fat JAR for AWS Lambda deployment
shadowJar {
    archiveFileName = "scare-scale-batch-job-all.jar"
    archiveClassifier = ""
    mergeServiceFiles()
}

// Make shadowJar part of the build
build.dependsOn shadowJar